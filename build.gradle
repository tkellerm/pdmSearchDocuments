plugins {
    id "groovy"
    id "maven-publish"
    id "com.palantir.git-version" version "0.5.3"
	id "de.abas.esdk" version "0.6.3"
	}

repositories {
    mavenLocal()
	jcenter()
    maven { url "http://$NEXUS_HOST:$NEXUS_PORT/nexus/content/repositories/$NEXUS_NAME" }
}

esdk {
    app {
        name="pdmDocumentsApp"
        vendorId="abasGmbH"
        appId="pdmDocumentsApp"
        entryPoints = []
        shared=true
        infosystems = ["IS.OWPDM.PDMDOCUMENTS"]
        tables = []
	    data = ["data.json"]
        keys = []
        enums = ["PAPIERFORMAT",  "PDMSYSTEMS"]
    }

    abas {
        homeDir = ABAS_HOMEDIR
        clientDir = ABAS_CLIENTDIR
        clientId = ABAS_CLIENTID
        edpHost = EDP_HOST
        edpPort = EDP_PORT.toInteger()
        edpUser = EDP_USER
        edpPassword = EDP_PASSWORD
    }

    nexus {
        nexusHost = NEXUS_HOST
        nexusPort = NEXUS_PORT.toInteger()
        nexusRepoName = NEXUS_NAME
        nexusPassword = NEXUS_PASSWORD
    }

    ssh {
        host = SSH_HOST
        port = SSH_PORT.toInteger()
        user = SSH_USER
        password = SSH_PASSWORD
        key = SSH_KEY
    }
    installType = "SSH"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

compileJava {
    options.encoding = "UTF-8"
}

configurations { compile.extendsFrom provided }

group='de.abasgmbh.pdm'
def versionString = gitVersion().toString()
if (versionString.contains("-")) {
    version versionString.split("-")[0].substring(1) + "-SNAPSHOT"
} else {
    version versionString.substring(1)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'abasGmbH'
            artifactId 'pdmSearchDocuments'
            artifact "build/libs/${project.name}-standalone-app.jar"
        }
    }
    repositories {
        if(project.hasProperty("nexusSnapshotURL") && project.hasProperty("nexusReleaseURL")
                && project.hasProperty("nexusUser") && project.hasProperty("nexusPassword")){
            if(project.version.endsWith('-SNAPSHOT')) {
                maven {
                    url nexusSnapshotURL
                    credentials {
                        username nexusUser
                        password nexusPassword
                    }
                }
            } else {
                maven {
                    url nexusReleaseURL
                    credentials {
                        username nexusUser
                        password nexusPassword
                    }
                }
            }
        }
    }
}

dependencies {
    provided 'de.abas.homedir:log4j:1.0.0'
	compile group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '3.1.2.Final'
    compile group: 'org.jboss.resteasy', name: 'resteasy-client', version: '3.1.2.Final'
    compile group: 'org.jboss.resteasy', name: 'resteasy-multipart-provider', version: '3.1.2.Final'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.7'
	compile group: 'com.itextpdf', name: 'itextpdf', version: '5.5.11'
	compile group: 'com.github.jai-imageio', name: 'jai-imageio-core', version: '1.3.1'
	compile 'log4j:log4j:1.2.17'
    
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    compile group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '6.2.1.jre8'
}
